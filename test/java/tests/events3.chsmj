/*
**      CHSM Language System
**      src/java/tests/events3.chsmj
**
**      Copyright (C) 2004-2013  Paul J. Lucas & Fabio Riccardi
**
**      This program is free software; you can redistribute it and/or modify
**      it under the terms of the GNU General Public License as published by
**      the Free Software Foundation; either version 3 of the License, or
**      (at your option) any later version.
** 
**      This program is distributed in the hope that it will be useful,
**      but WITHOUT ANY WARRANTY; without even the implied warranty of
**      MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
**      GNU General Public License for more details.
** 
**      You should have received a copy of the GNU General Public License
**      along with this program.  If not, see <http://www.gnu.org/licenses/>.
*/

/**
 * @file
 * Tests that base/derived parameter blocks do not interfere.
 */

%%
///////////////////////////////////////////////////////////////////////////////

chsm<Test> events3_machine is {
  event alpha( int value );
  event<alpha> beta;

  set s(p,q) is {
    cluster p(a,b) is {
      state a {
        alpha -> b %{
          //
          // Check that alpha's value should equal beta's and not alpha's.
          //
          if ( $param( alpha, value ) != 11 )
            exitCode = 2;
        %};
      }
      state b;
    }
    cluster q(a,b) is {
      state a {
        beta -> b %{
          //
          // This broadcast of alpha should be ignored because it's already in
          // progress via beta's inheritance.
          //
          alpha( 42 );

          //
          // Check that beta's value really was set.
          //
          if ( $param( beta, value ) != 11 )
            exitCode = 3;
        %};
      }
      state b;
    }
  }
}

///////////////////////////////////////////////////////////////////////////////
%%

class events3 {
  public static void main( String args[] ) {
    events3_machine m = new events3_machine();
    m.enter();
    m.beta( 11 );

    if ( !( m.active() &&
            m.s.active() &&
            m.s.p.active() && !m.s.p.a.active() && m.s.p.b.active() &&
            m.s.q.active() && !m.s.q.a.active() && m.s.q.b.active()
    ) )
      Test.exitCode = 1;

    Test.printResult();
  }
}

///////////////////////////////////////////////////////////////////////////////
// vim:set et sw=2 ts=2 syntax=java:
